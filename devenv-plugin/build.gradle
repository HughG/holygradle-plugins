buildscript {
    ext.kotlin_version = '1.1.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

configurations {
    optional
}
configurations.all {
    resolutionStrategy {
        // Need to force this because Gradle 3.5 provides 2.4.10 but gradle-script-kotlin depends on 2.4.7.
        force 'org.codehaus.groovy:groovy-all:2.4.10'
    }
}

repositories {
    maven { url 'https://repo.gradle.org/gradle/plugins-releases' }
}

dependencies {
    compile localGroovy()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.gradle:gradle-script-kotlin:0.8.0"
    ["custom-gradle-core", "intrepid"].each { pluginProjectName ->
        compile project(":${pluginProjectName}-plugin")
        optional project(path: ":${pluginProjectName}-plugin", configuration: 'optional')
        testCompile project(":${pluginProjectName}-plugin").sourceSets.test.output
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
