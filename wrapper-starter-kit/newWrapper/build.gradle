buildscript {
    gplugins.use "custom-gradle-core:${wrapperVersion}"
}
gplugins.apply()

/*
    If the plugins snapshot override is active, the snapshot version of custom-gradle-core-plugin
    will be used, which probably isn't what was intended.
*/
boolean usingPluginSnapshots = (System.getProperty('holygradle.pluginsSnapshotsUser') != null)
if (usingPluginSnapshots && !isSnapshotWrapperVersion) {
    throw new RuntimeException(
        "ERROR: holygradle.pluginsSnapshotsUser override is active but you are attempting to " +
        "publish a wrapper for a non-snapshot version (${wrapperVersion}).  This would instead " +
        "publish a wrapper using your snapshot version of Holy Gradle."
    )
}

/*
    Extra sanity check that we really are publishing with the version we asked for.  For example,
    it doesn't make sense to try to publish with a version number like "1.2.+".
*/
Map<String,String> pluginUsages = project.extensions.findByName("gplugins")?.usages as Map<String,String>
String customGradleCoreVersion = pluginUsages['custom-gradle-core']
if (wrapperVersion != customGradleCoreVersion) {
    throw new RuntimeException(
        "Requested custom-gradle-core:${wrapperVersion} but actual version is " +
        "${customGradleCoreVersion}"
    )
}

task clean.doLast {
    delete "gradlew", "gradlew.bat", "gw.bat", "gradle"
}

createWrapper.dependsOn clean

/*
    Copy files in from the root project (wrapper-starter-kit), which will itself have had them copied into it by the
    top-level build.gradle.  If you modify the copy patterns here, you should also update the ones near the start of
    that top-level build.gradle file.  These are only the site-local, private init files; the general ones will be
    written in by the "wrapper" task.
*/
createWrapper.doLast {
    copy {
        into(new File(projectDir, "gradle/wrapper"))
        from(new File(rootProject.projectDir, "gradle/wrapper")) {
            include "base-url-lookup.txt"
            include "proxy-lookup.txt"
            include "certs/**"
        }
    }
}