/*
    This project, separate from the root project hierarchy, is used for publishing a ZIP of the
    files needed to start using a given version of the custom-gradle distribution.

    Run it with "-PwrapperVersion=1.2.3.4" to publish a wrapper using custom-gradle-core-plugin
    version 1.2.3.4.  You also have to set environment variable WRAPPER_STARTER_KIT_VERSION to the
    version of the custom-gradle distribution to use to run this script.  This may or may not be the
    same as the version you want to publish.
*/

buildscript {
    gplugins.use "my-credentials:${wrapperVersion}"
}
gplugins.apply()
apply plugin: 'ivy-publish'

evaluationDependsOn("newWrapper")

final File localGroupFile = new File(projectDir, "../local/holy-gradle-plugins-wrapper-group.txt")
project.group = localGroupFile.exists() ? localGroupFile.text : "holygradle"
project.version = wrapperVersion

configurations {
    it."default"
}

String targetDir = "${project.projectDir}/newWrapper"

task makeZip(type: Zip) {
    dependsOn tasks.getByPath(":newWrapper:createWrapper")
    group = "Main"
    description = 'Creates a zip file containing the starter-kit.'
    destinationDir = project.buildDir
    baseName = project.name
    from("${targetDir}/gradle") {
        into("gradle")
        exclude("gradle-wrapper.properties")
    }
    from("${targetDir}/gw.bat")
}

artifacts {
    "default" makeZip
}

project.ext.PUBLISH_TARGET_URI = artifactoryServer + artifactoryPluginPublishRepo + "/"

publishing {
    repositories.ivy {
        credentials {
            username artifactoryUsername
            password artifactoryPassword
        }
        url PUBLISH_TARGET_URI
    }
}

project.tasks["publish"] << {
    println "Published ${project.name} ${project.version} " +
        "to ${PUBLISH_TARGET_URI} at: " + new Date()
    println "Once you have tested the wrapper, unzip it into this folder and commit the changes."
}
