buildscript {
    gplugins.use "intrepid:${System.getProperty("holygradle.versionForIntegrationTest")}"

    // Pull in JUnit so we can use org.junit.Assert
    dependencies {
        classpath "junit:junit:4.8.+"
    }
}
gplugins.apply()

import holygradle.scm.HgRepository
import holygradle.scm.SourceControlRepository

import static org.junit.Assert.*

apply from: "hgExec.gradle"

final String EXAMPLE_FILE = "ahoy.txt"

////////////////////////////////////////////////////////////////////////////////
// Set up this project as a Mercurial repo
//
// We need to do this in a separate Gradle run, because the "sourceControl" project extension is set
// up when the intrepid plugin is initialised.  It wouldn't be created/changed if we made the
// project folder into a repo during the same build where we're trying to run the test.

task setupRepo << {
    hgExec(project, "init")
}


////////////////////////////////////////////////////////////////////////////////
// Now do the tests.

task runTest << {

    def sourceControl = project.extensions.findByName("sourceControl") as SourceControlRepository

    assertTrue("An HgRepository instance has been created for the project", sourceControl instanceof HgRepository)

    // Add a file.
    (new File(project.projectDir, ".hgignore")).withPrintWriter { PrintWriter w ->
        w.println("^build/")
        w.println("^.*\\.gradle/")
    }
    hgExec(project, "add", ".hgignore")
    // Set the commit message, user, and date, so that the hash will be the same every time.
    hgExec(project,
           "commit",
           "-m", "Initial test state.",
           "-u", "TestUser",
           "-d", "2000-01-01"
    )

    assertEquals(
        "First commit hash is as expected",
        "cd7b5c688d1504b029a7286c2c0124c86b1d39a2",
        sourceControl.getRevision()
    )
    assertFalse("Initially there are no local changes", sourceControl.hasLocalChanges())

    // Add another file.
    (new File(project.projectDir, EXAMPLE_FILE)).text = "ahoy"
    hgExec(project, "add", EXAMPLE_FILE)
    // Set the commit message, user, and date, so that the hash will be the same every time.
    hgExec(project,
           "commit",
           "-m", "Added another file.",
           "-u", "TestUser",
           "-d", "2000-01-02"
    )

    assertEquals(
        "Second commit hash is as expected",
        "2fbc9b5207fda8a526ce38d6bb1ae208b175cd64",
        sourceControl.getRevision()
    )

    hgExec(project,
           "update",
           "-r", "cd7b5c688d1504b029a7286c2c0124c86b1d39a2",
    )

    assertEquals(
        "Updating to first commit hash is detected as expected",
        "cd7b5c688d1504b029a7286c2c0124c86b1d39a2",
        sourceControl.getRevision()
    )

    
}

[setupRepo, runTest].each { it.dependsOn setupHgExec }
