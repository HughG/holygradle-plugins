def pluginProjectNames =
    rootProject.allprojects.findAll { it.name.endsWith("-plugin") }.collect { ":" + it.name } 

dependencies {
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'

    // Project dependencies
    pluginProjectNames.each { pluginProjectName ->
        testCompile project(pluginProjectName).sourceSets.test.output
    }
    testCompile project(':custom-gradle-core-plugin')
}

/*
    Make sure we can compile and publish all plugins locally, before we even try to compile the
    integration test source.  If we can't, we won't be able to run the integration tests anyway, so
    it would be a waste of time.
*/
pluginProjectNames.each { pluginProjectName ->
    def pluginProject = rootProject.findProject(pluginProjectName)
    project.tasks["compileTestGroovy"].dependsOn pluginProject.tasks["publishPluginsLocally"]
}
